import com.github.gradle.node.npm.task.NpxTask

plugins {
    id "java"
    id "com.github.node-gradle.node" version "3.3.0"
}

group = 'ir.ac.kntu'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

node {
    version = '18.0.0'
    npmVersion = '8.6.0'
    download = true
}

def buildTask = tasks.register("buildClient", NpxTask) {
    command = "parcel"
    args = ["build", "--public-url", "static", "--no-cache", "--dist-dir", "${buildDir}/dist/public/static"]
    dependsOn(tasks.npmInstall)
    inputs.dir(project.fileTree("src"))
    inputs.dir("node_modules")
    outputs.dir("${project.buildDir}/dist/public")
}

def moveTask = tasks.register("moveIndex") {
    dependsOn(buildTask)
    onlyIf {
        project.file("${buildDir}/dist/public/static/index.html").exists()
    }
    doLast {
        ant.move file: project.file("${buildDir}/dist/public/static/index.html"),
                todir: project.file("${buildDir}/dist/templates")
    }
    outputs.dir("${buildDir}/dist")
}

sourceSets {
    java {
        main {
            resources {
                srcDir(moveTask)
            }
        }
    }
}